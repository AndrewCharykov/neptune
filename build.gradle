ext {
    globalVersion = '0.9.6-ALPHA'
}

subprojects {
    group 'ru.tinkoff.qa.neptune'
    version globalVersion

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'project-report'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    apply plugin: "java"

    repositories {
        mavenCentral()
    }

    ext {
        testNGVersion = '6.14.3'
        hamcrestVersion = '2.1'
        seleniumVersion = '3.141.59'
        allureVersion = '2.10.0'
        jacksonVersion = '2.9.8'
        mavenDeployLogin =  REPO_USER_NAME
        mavenDeployPassword = REPO_USER_PASSWORD
        globalVersion = version
    }

    dependencies {
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
        compile group: 'com.google.guava', name: 'guava', version: '27.0.1-jre'
        testCompile group: 'org.testng', name: 'testng', version: testNGVersion
        testCompile group: 'org.hamcrest', name: 'hamcrest', version: hamcrestVersion
        compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
        compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-jaxb-annotations', version: jacksonVersion
        compile (group: 'org.reflections', name: 'reflections', version: '0.9.11') {
            exclude group: 'com.google.guava'
            exclude group: 'com.google.code.gson'
        }
        compile group: 'commons-validator', name: 'commons-validator', version: '1.6'
        compile group: 'commons-io', name: 'commons-io', version: '2.6'
        compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    }

    test {
        useTestNG()
    }
}

task aggregatedJavadocs(type: Javadoc, description: 'Generate javadocs from all child projects as if it was a single project', group: 'Documentation') {
    title = "Neptune API Documentation. v$globalVersion"
    options.author true
    options.links 'http://download.oracle.com/javase/9/docs/api/'
    options.addStringOption 'Xdoclint:none', '-quiet'
    destinationDir  = file("${buildDir}/../../neptune.documentation/javadocs/")
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.bottom = "Copyright &copy; 2019 <a href=\"https://www.tinkoff.ru/software/\">Tinkoff.ru Development</a>."

    subprojects.each { proj ->
        proj.tasks.withType(Javadoc).each { javadocTask ->
            source += javadocTask.source
            classpath += javadocTask.classpath
            excludes += javadocTask.excludes
            includes += javadocTask.includes
        }
    }
}
